plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
    id "com.modrinth.minotaur" version "2.8.10"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/releases/" }
}

loom {
	splitEnvironmentSourceSets()
	mods {
		"fpshud" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modApi "com.terraformersmc:modmenu:${modmenu_version}"
	modApi("me.shedaniel.cloth:cloth-config-fabric:${clothConfigApi_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
}

processResources {
	inputs.property "version", project.version
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "2iH7sx2j"
    versionNumber = "${project.version}"
    versionName = "FPS hud ${project.version}"
    changelog = "[Version ${project.version}](https://github.com/Flavio6561/FPShud/wiki/Version-changelogs)"
    uploadFile = remapJar
    versionType = "release"
    gameVersions = ["1.21.9", "1.21.10", "1.21.11"]
    loaders = ["fabric", "quilt"]

    dependencies {
        required.project "P7dR8mSH"
        optional.project "mOgUt4GM"
        optional.project "9s6osm5g"
    }
}

tasks.modrinth.dependsOn(tasks.remapJar)